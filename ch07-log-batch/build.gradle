plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'kr.co.wikibook.batch'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-batch'
	implementation 'org.springframework.boot:spring-boot-starter-json'
	implementation 'org.springframework:spring-oxm'
	implementation 'org.glassfish.jaxb:jaxb-runtime'

	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.batch:spring-batch-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

task h2(type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	mainClass = 'org.h2.tools.Console'

	def props = new Properties()
	file("src/main/resources/application.properties").withInputStream { props.load(it) }

	args(
			'-driver', props.get('spring.datasource.driver-class-name'),
			'-url', props.get('spring.datasource.url'),
			'-user', props.get('spring.datasource.username')
	)
}
