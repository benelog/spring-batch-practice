plugins {
    id 'batch-modules.spring-lib-deps'
}

dependencies {
    api 'org.slf4j:slf4j-api'
    implementation 'ch.qos.logback:logback-classic'
    api 'org.springframework.batch:spring-batch-core'
    implementation 'com.zaxxer:HikariCP'
    runtimeOnly 'com.h2database:h2'
}

tasks.register('jobDb', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.h2.tools.Console'

    def props = new Properties()
    file("src/main/resources/job-db.properties").withInputStream { props.load(it) }

    args(
            '-driver', props.get('job-db.driver-class-name'),
            '-url', props.get('job-db.jdbc-url'),
            '-user', props.get('job-db.username')
    )
}
