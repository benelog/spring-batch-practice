plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'kr.co.wikibook.batch'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.cloud:spring-cloud-starter-task'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('h2', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.h2.tools.Console'

    def props = new Properties()
    file('src/main/resources/application.properties').withInputStream { props.load(it) }

    args(
            '-driver', props['spring.datasource.driver-class-name'],
            '-url', props['spring.datasource.url'],
            '-user', props['spring.datasource.username']
    )
}
